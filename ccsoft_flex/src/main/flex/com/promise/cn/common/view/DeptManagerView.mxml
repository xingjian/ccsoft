<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" creationComplete="init()"
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.promise.cn.common.vo.Dept;
			import com.promise.cn.framework.util.Global;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.remoting.mxml.RemoteObject;
			
			[Bindable]
			private var deptList:ArrayCollection;
			private var deptServiceList:RemoteObject;
			private var deptServiceDelete:RemoteObject;
			
			//初始化方法
			public function init():void{
				deptServiceList = Global.getRemoteObject("deptService",deptServiceListHandle);
				deptServiceDelete = Global.getRemoteObject("deptService",deptServiceDeleteHandle);
				deptServiceList.getDeptsTreeList();
				initTreeContextMenu();
			}
			
			//初始化树的右键菜单
			public function initTreeContextMenu():void{
				var contextMenu:ContextMenu = new ContextMenu();
				contextMenu.hideBuiltInItems();
				var contextMenuItems:Array = [];
				var addMenuItem:ContextMenuItem = new ContextMenuItem("新增下级节点信息");
				addMenuItem.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT,addDept);
				var removeMenuItem:ContextMenuItem = new ContextMenuItem("删除节点信息");
				removeMenuItem.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT,removeDept);
				contextMenuItems.push(addMenuItem);
				contextMenuItems.push(removeMenuItem);
				contextMenu.customItems = contextMenuItems;
				this.deptTree.contextMenu = contextMenu;
			}
			
			//新增加菜单
			public function addDept(event:ContextMenuEvent):void{
				rightGroup.removeAllElements();
				var addDept:AddDept = new AddDept();
				addDept.parentDept = deptTree.selectedItem as Dept;
				rightGroup.addElement(addDept);
			}
			
			//删除节点信息
			public function removeDept(event:ContextMenuEvent):void{
				var deptTemp:Dept = deptTree.selectedItem as Dept;
				deptServiceDelete.deleteDept(deptTemp.id);
			}
			
			//删除菜单成功处理
			public function deptServiceDeleteHandle(event:ResultEvent):void{
				deptServiceList.getDeptsTreeList();
				Alert.show("删除部门成功！");
			}
			
			//获取菜单树成功
			public function deptServiceListHandle(event:ResultEvent):void{
				deptList = event.result as ArrayCollection;
			}
		]]>
	</fx:Script>
	<s:HGroup width="100%" height="100%">
		<mx:Tree id="deptTree" width="25%" height="100%" dataProvider="{deptList}" labelField="name" />
		<s:Group id="rightGroup" width="75%" height="100%">
		</s:Group>
	</s:HGroup>
</s:Group>
